WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "#" ~ " "? ~ comment_inner ~ (NEWLINE | EOI) }

comment_inner = { (!NEWLINE ~ ANY)* }

identifier = @{ tag }
tag = @{ (ASCII_ALPHANUMERIC | LETTER | "_" | "-" | "." | "'" | "â€™")+ }

object = {
    "{" ~ "}" |
    "{" ~ (inequality | pair)* ~ "}"
}

pair = { identifier ~ "=" ~ value }

inequality_operator = @{ ">=" | "<=" | ">" | "<" }
inequality = { identifier ~ inequality_operator ~ value }

array = {
    "{" ~ "}" |
    "{" ~ value* ~ "}"
}

value = _{ object | array | date | string | number | boolean | tag }

boolean = @{ ("yes" | "no") ~ !ASCII_ALPHA }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{ float | int }

int = @{"-"? ~ ASCII_DIGIT+}
float = @{int? ~ "." ~ ASCII_DIGIT+ ~ "f"?}

date = ${ ("\"" ~ date_inner ~ "\"") | date_inner }
date_inner = @{ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

body = { pair* }
file = _{
    SOI ~
    body ~
    EOI
}