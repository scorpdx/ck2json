WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

header = { "CK2txt" }

identifier = @{ (ASCII_ALPHA | "_")+ }

object = {
    "{" ~ "}" |
    "{" ~ pair* ~ "}"
}

pair = { identifier ~ "=" ~ value }

array = {
    "[" ~ "]" |
    "[" ~ value* ~ "]"
}

value = _{ object | array | date | string | number | boolean }

boolean = { "yes" | "no" }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = { float | int }

int = @{"-"? ~ ASCII_DIGIT+}
float = @{int? ~ "." ~ ASCII_DIGIT+ ~ "f"?}

date = _{("\"" ~ date_inner ~ "\"") | date_inner}
date_inner = @{ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

file = _{
    SOI ~
    header ~
    pair* ~
    "}" ~ pair ~
    EOI
}