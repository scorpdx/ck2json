WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

header = { "CK2txt" }

identifier = { tag }
tag = @{ (ASCII_ALPHA | "_")+ | ASCII_DIGIT+ }

object = {
    "{" ~ "}" |
    "{" ~ pair* ~ "}"
}

pair = { identifier ~ "=" ~ value }

array = {
    "{" ~ "}" |
    "{" ~ value* ~ "}"
}

value = _{ object | array | date | string | number | boolean | tag }

boolean = @{ ("yes" | "no") ~ !ASCII_ALPHA }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = { float | int }

int = @{"-"? ~ ASCII_DIGIT+}
float = @{int? ~ "." ~ ASCII_DIGIT+ ~ "f"?}

date = { date_string | date_inner}
date_string = ${ "\"" ~ date_inner ~ "\"" }
date_inner = @{ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

file = _{
    SOI ~
    header ~
    pair* ~
    "}" ~ pair ~
    EOI
}